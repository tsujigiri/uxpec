(
  A collection of tests to be performed on the stack

  Consumes the result of some operation from the stack and prints an indication
  of whether the test was successful or not according to the assertion used.

  <result> --
)
@assert
	( check for a non-zero byte on the stack )
	&true
		?&success
		!&fail

	&success
		;assert-success-symbol uxpec-print
		JMP2r

	&fail
		#01 ;assert-status STA
		;assert-fail-symbol uxpec-print
		JMP2r

@uxpec-exit
	;assert-status LDA ?&fail !&succeed
	&fail #010f DEO BRK
	&succeed #800f DEO BRK

@assert-success-symbol 20 f0 9f 91 8c #0a 00
@assert-fail-symbol    20 f0 9f 94 a5 #0a 00
@assert-status 00


@describe
	STH2r
	#0a .Console/write DEO ( \n )
	uxpec-print
	#0a .Console/write DEO ( \n )
	#01 ;uxpec-indent-level STA
	JMP2

@context
	STH2r
	uxpec-indent
	uxpec-print
	#0a .Console/write DEO ( \n )
	;uxpec-indent-level LDA INC ;uxpec-indent-level STA
	JMP2

( prints the address of the describe string and its own )
@it
	STH2r
	uxpec-indent
	uxpec-print
	JMP2

(
	prints a null-terminated string from the absolute address on the stack and
	jumps back to the address on the return stack
)
@uxpec-print
	&loop
		LDAk DUP ?&print-char
		POP INC2 JMP2r
	&print-char
		.Console/write DEO INC2 !&loop

@uxpec-indent
  ;uxpec-indent-level LDA
  &print-tab #09 .Console/write DEO #01 SUB DUP ?&print-tab
  POP JMP2r

@uxpec-indent-level 00

