(
  A collection of tests to be performed on the stack

  Consumes the result of some operation from the stack and prints an indication
  of whether the test was successful or not according to the assertion used.

  <result> --
)
@assert
	( check for a non-zero byte on the stack )
	&true
		?&success
		!&fail

	&false
		?&fail
		!&success

	&equ
		EQU
		!&true

	&equ2
		EQU2
		!&true

	&stack-empty
		;assert/stack-empty-catch uxpec/set-system-vector
		POPk
		uxpec/restore-system-vector
		!&fail
		&stack-empty-catch
			uxpec/restore-system-vector
			POP2 POP2
			!&success

	&success
		!&return

	&fail
		#01 ;uxpec/assert-failed STA
		#01 ;uxpec/assert-failed-global STA
		!&return

	&return
		#01 ;uxpec/assert-called STA
		JMP2r

@uxpec-exit
	;uxpec/assert-failed-global LDA ?&fail !&succeed
	&fail #010f DEO BRK
	&succeed #800f DEO BRK

@describe
@context
	uxpec/indent
	STH2r uxpec/print
	#0a18 DEO ( \n )
	;uxpec/indent-level LDA INC ;uxpec/indent-level STA
	JMP2

( prints the address of the describe string and its own )
@it
	uxpec/indent
	STH2r uxpec/print
	;uxpec/indent-level LDA INC ;uxpec/indent-level STA
	JMP2

@end
	;uxpec/indent-level LDA #01 SUB ;uxpec/indent-level STA
	;uxpec/assert-called LDA ?&indicate-status !&return
	&indicate-status
		#00 ;uxpec/assert-called STA
		;uxpec/assert-failed LDA ?&fail !&success
		&fail ;uxpec/assert-fail-symbol uxpec/print POP2 !&return
		&success ;uxpec/assert-success-symbol uxpec/print POP2

	&return
		#00 ;uxpec/assert-failed STA
		JMP2r

@uxpec
	&assert-success-symbol 20 f0 9f 91 8c 0a 00
	&assert-fail-symbol    20 f0 9f 94 a5 0a 00
	&assert-failed 00
	&assert-called 00
	&assert-failed-global 00


	(
		prints a null-terminated string from the absolute address on the stack and
		jumps back to the address on the return stack
	)
	&print
		LDAk DUP ?&print-char
		POP INC2 JMP2r
		&print-char
			#18 DEO INC2 !&print

	&indent
		,&indent-level LDR
		&indent-loop
			DUP #00 EQU ?&return
			#0918 DEO ( \t )
			#01 SUB
			!&indent-loop
		&return POP JMP2r

	&indent-level 00

	&set-system-vector
		#00 DEI2 ,&system-vector-backup STR2
		#00 DEO2
		JMP2r

	&restore-system-vector
		,&system-vector-backup LDR2 #00 DEO2
		JMP2r

	&system-vector-backup 0000

	&create-stack-snapshot
		,&next-stack-stack-addr JSR
		DUP2 ,&stack-stack-ptr-old STR2
		;uxpec/create-stack-snapshot-catch ,&set-system-vector JSR
		&create-stack-snapshot-loop
			STAk INC2 DUP2 ,&stack-stack-ptr STR2 ROT POP
			!&create-stack-snapshot-loop
		&stack-stack-ptr-old $2
		&create-stack-snapshot-catch
			,&restore-system-vector JSR
			POP2 POP2
			,&stack-stack-ptr LDR2 DUP2 ,&stack-stack-ptr-old LDR2
			SUB2 SWP2 STA POP
	&copy-snapshot-to-stack
		;uxpec/stack-stack-ptr LDA2
		LDAk STH ( load stack size and move to return stack )
		&restore-stack-loop
			STHkr #00 EQU ?&skip-restore-stack-loop
			#0001 SUB2 ( decrement stack ptr )
			LDAk ROT ROT ( push value onto stack )
			LITr 01 SUBr
			!&restore-stack-loop ( decrement stack size counter )
		&skip-restore-stack-loop
		POPr POP2 JMP2r

	&next-stack-stack-addr
		,&stack-stack-ptr LDR2
		DUP2 #0000 EQU2 ?&init-stack-stack-ptr
		INC2
		JMP2r
		&init-stack-stack-ptr
			POP2 ;uxpec/stack-stack
			DUP2 ,&stack-stack-ptr STR2
		JMP2r

	&stack-stack-ptr $2

	&roll-back-stack
		;uxpec/roll-back-stack-catch ,&set-system-vector JSR
		&roll-back-stack-loop POP !&roll-back-stack-loop
		&roll-back-stack-catch
			,&restore-system-vector JSR
			POP2 POP2
			,&copy-snapshot-to-stack JSR
			( subtract stack size from stack pointer )
			;uxpec/stack-stack-ptr LDA2k LDAk #00 SWP SUB2 #0001 SUB2  ( SWP2 STA2 )
			DUP2 ;uxpec/stack-stack LTH2 ?&roll-back-stack-zero-ptr SWP2 STA2
			JMP2r
			&roll-back-stack-zero-ptr POP2 #0000 SWP2 STA2
			JMP2r

			;uxpec/stack-stack-ptr LDA2 #000e DEO POP2

	&stack-stack $1024
